// For building the open-source release of TalkBack.

apply plugin: 'com.android.application'

apply from: 'shared.gradle'

apply from: 'version.gradle'

final BUILD_TIMESTAMP = new Date().format('yyyy_MM_dd_HHmm')

dependencies {
    implementation project(':talkback')
}

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
        classpath 'com.android.tools.build:gradle:8.3.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    namespace talkbackApplicationId

    buildToolsVersion '34.0.0'
    defaultConfig {
        applicationId talkbackApplicationId
        versionName talkbackVersionName + "-" + BUILD_TIMESTAMP
        minSdkVersion 26
        targetSdkVersion 34
        compileSdkVersion 34
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    productFlavors {
        phone {
            dimension "target"
        }
        wear {
            dimension "target"
            versionNameSuffix "-wear"
        }
    }
}
